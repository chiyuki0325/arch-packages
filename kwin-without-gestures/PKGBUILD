# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kirikaze Chiyuki <me@chyk.ink>

pkgname=kwin-without-gestures
_pkgname=kwin
pkgver=6.1.3
_dirver=$(echo $pkgver | cut -d. -f1-3)
provides=('kwin')
conflicts=('kwin')
pkgrel=1
pkgdesc='An easy to use, but flexible, composited Window Manager (without touchpad gestures)'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL-2.0-or-later)
depends=(breeze
         gcc-libs
         glibc
         plasma-activities
         kauth
         kcmutils
         kcolorscheme
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         kdeclarative
         kdecoration
         kglobalaccel
         kglobalacceld
         kguiaddons
         ki18n
         kidletime
         kirigami
         kitemmodels
         knewstuff
         knotifications
         kpackage
         kquickcharts
         kscreenlocker
         kservice
         ksvg
         kwayland
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         lcms2
         libdisplay-info
         libdrm
         libepoxy
         libinput
         libpipewire
         libqaccessibilityclient-qt6
         libx11
         libxcb
         libxcvt
         libxi
         libxkbcommon
         libxkbcommon-x11
         mesa
         pipewire-session-manager
         python
         libplasma
         qt6-5compat
         qt6-base
         qt6-declarative
         qt6-multimedia
         qt6-sensors
         qt6-tools
         qt6-wayland
         systemd-libs
         wayland
         xcb-util-cursor
         xcb-util-keysyms
         xcb-util-wm)
makedepends=(extra-cmake-modules
             kdoctools
             krunner
             plasma-wayland-protocols
             python
             wayland-protocols
             xorg-xwayland)
optdepends=('maliit-keyboard: virtual keyboard for kwin-wayland')
groups=(plasma)
source=(
  https://download.kde.org/stable/plasma/$_dirver/$_pkgname-$pkgver.tar.xz
  0001-kwin-disable-gestures.diff
)
install=$pkgname.install
sha256sums=(
  'fb23b9d085381ba9455cd3f3aca184b137a7653951ca9f7b78f9e746dd0cc6bf'
  '065fda75119d86e9d7b1596784d3db004aceafedba075a05482b01dcc745c877'
)

build() {
  msg2 "Applying patch..."
  pushd $_pkgname-$pkgver
  patch -p0 < $srcdir/0001-kwin-disable-gestures.diff
  popd
  cmake -B build  -S $_pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
