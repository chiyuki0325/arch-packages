# Maintainer: sukanka <su975853527 at gmail.com>
# Contributor: Jonas BÃ¶gle <aur@iwr.sh>
# Contributor: Jonathan Duck <duckbrain30@gmail.com>
# Contributor: Yidaozhan Ya <yidaozhan_ya@outlook.com>

pkgname=typora-electron-hooked
_pkgname=typora
pkgver=1.6.6
_electron=electron
pkgrel=1
pkgdesc="A minimal markdown editor and reader. (Typora with system electron and NodeInject hook)"
arch=('x86_64' 'aarch64')
license=('custom:"Copyright (c) 2015 Abner Lee All Rights Reserved."')
url="https://typora.io"
provides=('typora')
conflicts=('typora' 'typora-free' 'typora-electron')
depends=('ripgrep' "${_electron}")
optdepends=(
	'noto-fonts-emoji: Or some other emoji font to see emojis'
	'pandoc: Import/export for extra file formats'
)
makedepends=("rust")
source=(
	"typora.sh"
	"git+https://github.com/DiamondHunters/NodeInject.git"
	"git+https://github.com/DiamondHunters/NodeInject_Hook_example.git"
)
source_x86_64=("${_pkgname}_${pkgver}_amd64.deb::${url}/linux/${_pkgname}_${pkgver}_amd64.deb")
source_aarch64=("${_pkgname}_${pkgver}_arm64.deb::${url}/linux/${_pkgname}_${pkgver}_arm64.deb")
sha512sums=(
	'0fac707616e055158d4a84a80554db1b4c76e44604de74f1e0726955d7b45e0ec5560cd5c3994b97d24d9f7f2a1bdc9a1af50a4fbc70aa22076d19079910c304'
	'SKIP'
	'SKIP'
)
sha512sums_x86_64=('9cd83f9385b6332cf8a0231aebca4f477d4336ceeefba7292a22f63c327a54ede40aa73ba010ae581ce90ff06b10e47d5e3ef2dae067d929329261bc16b0772c')
sha512sums_aarch64=('b162d67c873c34245b810474c7dbde0dfe60e864e81841b38a145fba1a6d99c92239ffc3b0ac92119e8e85231051fd0b94f200da3edc018fa3c29f884bb46387')

prepare() {
	bsdtar -xf data.tar.xz -C "$srcdir/"
	sed -i "s|__ELECTRON__|${_electron}|" ${srcdir}/${_pkgname}.sh
}

build() {
	# Build NodeInject
	cd "${srcdir}/NodeInject"
	cp "${srcdir}/NodeInject_Hook_example/hook.js" "${srcdir}/NodeInject/src/hooklog.js"
	sed -i "s#/resources/#/#g" src/main.rs
	cargo build --release

	# Build keygen
	cd "${srcdir}/NodeInject_Hook_example/license-gen"
	cargo build --release
}
package() {
	# Package typora
	cd "$srcdir"
	cp -rf "usr" "${pkgdir}"
	install -Dm644 "$pkgdir/usr/share/doc/$_pkgname/copyright" "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
	mkdir -p ${pkgdir}/usr/lib/typora
	mv ${pkgdir}/usr/share/typora/resources/* ${pkgdir}/usr/lib/typora
	install -Dm755 ${srcdir}/${_pkgname}.sh ${pkgdir}/usr/bin/${_pkgname}
	ln -sf /usr/bin/rg ${pkgdir}/usr/lib/${_pkgname}/node_modules/vscode-ripgrep/bin/rg

	rm -rf ${pkgdir}/usr/share/{doc,lintian,typora}

	mv "${srcdir}/NodeInject/target/release/node_inject" "${pkgdir}/usr/lib/${_pkgname}/"
	pushd "${pkgdir}/usr/lib/${_pkgname}/"
		"${pkgdir}/usr/lib/${_pkgname}/node_inject"
	popd
	rm "${pkgdir}/usr/lib/${_pkgname}/node_inject"
	rm -rf "${pkgdir}/usr/lib/${_pkgname}/node"

	mv "${srcdir}/NodeInject_Hook_example/license-gen/target/release/license-gen" "${pkgdir}/usr/bin/typora-license-gen"
	msg2 "$("${pkgdir}/usr/bin/typora-license-gen")"
	msg2 "Run typora-license-gen to generate a new license serial"
}
